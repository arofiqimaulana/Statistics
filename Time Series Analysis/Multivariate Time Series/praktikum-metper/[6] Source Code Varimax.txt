*Import data;
proc import out=work.varimax datafile='D:\BANK DATA\DATABASE\VARIMAX\Adv-Sales.xlsx' 
dbms=xlsx replace;
sheet='sheet1';
getnames=yes;
run;
*Print data;
proc print data=varimax;
run;
*plot data time series;
proc timeseries data=varimax vectorplot=series;
id Date interval=year;
var Advertising Sales;
run;
*manipulasi data;
data varimax;
set varimax;
z=0;
tx=log(Advertising);
ty=log(Sales);
run;
*Stasioneritas ragam;
*transformasi box cox;
proc transreg maxiter=0 nozeroconstant;
      model BoxCox(Sales) = identity(z);
      output;
run;
*Stasioneritas rata-rata;
proc arima data=varimax;
identify var=tx(1) nlag=40 stationarity=(adf);
identify var=ty(1) nlag=40 stationarity=(adf);
run;
*Penentuan Lag Optimum;
proc varmax data=varimax;
model tx ty/ dify(1) dftest lagmax=10 
	noint minic=(p=5 q=5);
output;
run;
*Korelasi Silang (CCF);
proc arima data=varimax;
identify var=tx(1) crosscorr=ty(1) nlags=30;
run;
*Causalitas Granger;
proc varmax data=varimax;
	model tx ty / p=1 noprint;
	causal group1=(tx) group2=(ty);
	causal group1=(ty) group2=(tx);
run;
*Kointegresi Johansen;
proc varmax data=varimax;
	model tx ty / p=3 noint dify(1) dftest cointtest=(johansen);
run;
*Identifikasi model VARIMA ;
proc varmax data=varimax;
model tx ty/
	dify(1) dftest lagmax=10 
	noint minic=(p=5 q=5) print=(corry pcorr);
output;
run;
*Pembentukan model VARIMA dan diagnosa model;
proc varmax data=varimax;
model tx ty / p=1
	dify(1) dftest lagmax=10
	noint print=(corry pcorr parcoef estimates diagnose);
output;
run;

